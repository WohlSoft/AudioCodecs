cmake_minimum_required (VERSION 3.2)
project(AudioCodecs VERSION 1.0.0 LANGUAGES C CXX)

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

if(POLICY CMP0058) # Dependencies
    cmake_policy(SET CMP0058 NEW)
endif()

if(POLICY CMP0077) # Cache variables override since 3.12
    cmake_policy(SET CMP0077 NEW)
endif()

option(CODECS_BUILD_SHARED "Build shared libraries if possible" OFF)
option(CODECS_BUILD_STATIC "Build static libraries if possible" ON)

option(DOWNLOAD_SDL2_DEPENDENCY "Download, compile, and install SDL2's master branch" OFF)
option(USE_LOCAL_SDL2 "Build SDL2 from local copy" OFF)
set(SDL2_REPO_PATH "" CACHE PATH "Path to the SDL2")

if(${CMAKE_SYSTEM_NAME} STREQUAL "Emscripten")
    set(EMSCRIPTEN 1)
    message("AudioCodecs using Emscripten!")
endif()

if(NOT SDL2_REPO_PATH)
    # Try to resolve sqlite dependency
    if(DOWNLOAD_SDL2_DEPENDENCY OR USE_LOCAL_SDL2)
        option(BUILD_SDL2_SHARED "Enable shared build of SDL2" ON)
        option(BUILD_SDL2_STATIC "Enable static build of SDL2" ON)
        # Download and configure SDL2 dependency
        include(download_sdl2_hg.cmake)
        set(SDL2_REPO_PATH ${SDL2_INSTALL_DIR})
    endif()
endif()

link_directories(${CMAKE_BINARY_DIR}/lib)

if(SDL2_REPO_PATH)
    link_directories(${SDL2_INSTALL_DIR}/lib)
    include_directories(${SDL2_INSTALL_DIR}/include)
    message("SDL2 Repo folder is ${SDL2_REPO_PATH}")
endif()

# --- TODO: implement building or usage of pre-built libSDL here ---
# set(SNDIO OFF) # Disable sndio that is not needed to us
# add_subdirectory(libSDL2)

option(BUILD_OGG_VORBIS "Enable building of OGG and Vorbis" ON)
if(BUILD_OGG_VORBIS)
    add_subdirectory(libogg)
    add_subdirectory(libvorbis)
    add_dependencies(vorbis ogg)
    add_dependencies(vorbisfile vorbis ogg)
    add_dependencies(vorbisenc vorbis ogg)

    add_subdirectory(tremor)
    add_dependencies(vorbisidec ogg)
endif()

option(BUILD_FLAC "Enable building of FLAC" ON)
if(BUILD_FLAC)
    add_subdirectory(libFLAC)
endif()

option(BUILD_ID3TAGSDL "Enable building of libID3TAG-SDL" OFF)
if(BUILD_ID3TAGSDL)
    add_subdirectory(libid3tag-sdl)
    if(DOWNLOAD_SDL2_DEPENDENCY OR USE_LOCAL_SDL2)
        add_dependencies(id3tag SDL2HG)
    endif()
endif()

option(BUILD_MP3_MAD "Enable building of libMAD" ON)
if(BUILD_MP3_MAD)
    add_subdirectory(libmad)
endif()

option(BUILD_MP3_SMPEG "Enable building of libSMPEG" OFF)
if(BUILD_MP3_SMPEG)
    add_subdirectory(smpeg)
    if(DOWNLOAD_SDL2_DEPENDENCY OR USE_LOCAL_SDL2)
        add_dependencies(smpeg SDL2HG)
    endif()
endif()

option(BUILD_GME "Enable building of GME" ON)
if(BUILD_GME)
    set(BUILD_SHARED_LIBS ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    set(BUILD_GME_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    set(BUILD_GME_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    mark_as_advanced(BUILD_SHARED_LIBS BUILD_GME_STATIC BUILD_GME_SHARED)
    add_subdirectory(zlib)
    add_subdirectory(libgme)
    if(BUILD_GME_STATIC)
        add_dependencies(gme_static zlibstatic)
    endif()
    if(BUILD_GME_SHARED)
        add_dependencies(gme_shared zlibstatic)
    endif()
    unset(BUILD_SHARED_LIBS)
    unset(BUILD_GME_STATIC)
    unset(BUILD_GME_SHARED)
endif()

option(BUILD_TIMIDITYSDL "Enable building of Timidity-SDL" ON)
if(BUILD_TIMIDITYSDL)
    add_subdirectory(libtimidity-sdl)
endif()

option(BUILD_MODPLUG "Enable building of libModPlug" ON)
if(BUILD_MODPLUG)
    set(MODPLUG_BUILD_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    set(MODPLUG_BUILD_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    mark_as_advanced(MODPLUG_BUILD_SHARED MODPLUG_BUILD_STATIC)
    add_subdirectory(libmodplug)
    unset(MODPLUG_BUILD_SHARED)
    unset(MODPLUG_BUILD_STATIC)
endif()

if(NOT ANDROID)
    option(BUILD_MIKMOD "Enable building of libMikMod" ON)
endif()
if(BUILD_MIKMOD)
    set(ENABLE_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    set(ENABLE_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    set(ENABLE_DOC 0 CACHE BOOL "" FORCE)
    mark_as_advanced(ENABLE_SHARED ENABLE_STATIC ENABLE_DOC)
    add_subdirectory(libmikmod)
    unset(ENABLE_DOC)
    unset(ENABLE_SHARED)
    unset(ENABLE_STATIC)
endif()

option(BUILD_LIBXMP "Enable building of libXMP" ON)
if(BUILD_LIBXMP)
    set(BUILD_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    set(BUILD_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    mark_as_advanced(BUILD_SHARED BUILD_STATIC)
    add_subdirectory(libxmp)
    unset(BUILD_SHARED)
    unset(BUILD_STATIC)
endif()

option(BUILD_OPUS "Enable building of OPUS" ON)
if(BUILD_OPUS)
    if(NOT BUILD_OGG_VORBIS)
        add_subdirectory(libogg)
    endif()
    add_subdirectory(libopus)
    add_subdirectory(libopusfile)
    target_include_directories(opus PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
    target_include_directories(opusfile PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
    add_dependencies(opus ogg)
    add_dependencies(opusfile opus ogg)
endif()

option(BUILD_FLUIDLITE "Enable building of FluidLite" ON)
if(BUILD_FLUIDLITE)
    # if(NOT BUILD_OGG_VORBIS) # // Enable in condition of SF3 support usage
    #     message(FATAL_ERROR "You should enable the build of the Vorbis to get use the FluidLite")
    # endif()
    set(FLUIDLITE_BUILD_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    set(FLUIDLITE_BUILD_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    add_subdirectory(FluidLite)
    if(FLUIDLITE_BUILD_STATIC)
        target_include_directories(fluidlite-static PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
        target_include_directories(fluidlite-static PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
        target_include_directories(fluidlite-static PUBLIC ${CMAKE_SOURCE_DIR}/libvorbis/include)
        target_include_directories(fluidlite-static PUBLIC ${CMAKE_SOURCE_DIR}/libvorbis/include)
        add_dependencies(fluidlite-static ogg vorbis)
    endif()
    if(FLUIDLITE_BUILD_SHARED)
        target_include_directories(fluidlite PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
        target_include_directories(fluidlite PUBLIC ${CMAKE_SOURCE_DIR}/libogg/include)
        target_include_directories(fluidlite PUBLIC ${CMAKE_SOURCE_DIR}/libvorbis/include)
        target_include_directories(fluidlite PUBLIC ${CMAKE_SOURCE_DIR}/libvorbis/include)
        add_dependencies(fluidlite ogg vorbis)
    endif()
endif()

if(NOT MSVC)
    # For now this is not buildable under MSVC yet :-(
    option(BUILD_OPENMPT "Enable building of libOpenMPT" OFF)
    if(BUILD_OPENMPT)
        add_subdirectory(libopenmpt)
    endif()
endif()

option(BUILD_ADLMIDI "Enable building of libADLMIDI" ON)
if(BUILD_ADLMIDI)
    set(libADLMIDI_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    set(libADLMIDI_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    mark_as_advanced(libADLMIDI_SHARED libADLMIDI_STATIC)
    add_subdirectory(libADLMIDI)
    unset(libADLMIDI_STATIC)
    unset(libADLMIDI_SHARED)
endif()

option(BUILD_OPNMIDI "Enable building of libOPNMIDI" ON)
if(BUILD_OPNMIDI)
    set(libOPNMIDI_STATIC ${CODECS_BUILD_STATIC} CACHE BOOL "" FORCE)
    set(libOPNMIDI_SHARED ${CODECS_BUILD_SHARED} CACHE BOOL "" FORCE)
    mark_as_advanced(libOPNMIDI_STATIC libOPNMIDI_SHARED)
    add_subdirectory(libOPNMIDI)
    unset(libOPNMIDI_STATIC)
    unset(libOPNMIDI_SHARED)
endif()
